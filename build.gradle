plugins {
  id 'java'
  id 'eclipse'
  id 'maven'
  id 'signing'
}

project.sourceCompatibility = '1.8'
project.group = 'com.hectorlopezfernandez.pebble'
project.name = 'spring-security-extension'
project.version = '2.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'javax.servlet:javax.servlet-api:3.1.0'
  implementation 'org.springframework:spring-web:4.3.5.RELEASE'
  implementation 'org.springframework.security:spring-security-web:4.1.4.RELEASE'
  implementation 'com.mitchellbosecke:pebble:2.3.0'
  implementation 'com.mitchellbosecke:pebble-spring4:2.3.0'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.springframework.boot:spring-boot-starter-web:1.4.3.RELEASE'
  testImplementation 'org.springframework.boot:spring-boot-starter-security:1.4.3.RELEASE'
  testImplementation 'org.springframework.boot:spring-boot-starter-test:1.4.3.RELEASE'
  testImplementation 'org.springframework.security:spring-security-test:4.1.4.RELEASE'
  testImplementation 'com.mitchellbosecke:pebble-spring-boot-starter:2.3.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
        authentication(userName: mavenUser, password: mavenPassword)
      }
      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
        authentication(userName: mavenUser, password: mavenPassword)
      }
      pom.project {
        name 'Spring Security extension for Pebble Template Engine'
        description "Pebble Template Engine extension supporting the functionality of Spring Security's jsp tags"
        url 'https://github.com/hectorlf/pebble-joda-time-extension'
        licenses {
          license {
            name "The MIT License (MIT)"
            url 'https://opensource.org/licenses/MIT'
          }
        }
        scm {
          url 'https://github.com/hectorlf/pebble-spring-security-extension'
        }
        developers {
          developer {
            name 'Hector Lopez'
            email 'postmaster@hectorlopezfernandez.com'
            url 'https://www.hectorlopezfernandez.com'
          }
        }
      }
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }
    }
  }
}

signing {
   required { gradle.taskGraph.hasTask("uploadArchives") }
   sign configurations.archives
}
